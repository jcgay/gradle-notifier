buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
        classpath 'org.ajoberstar:gradle-git:1.2.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.bmuschko.nexus'

group = 'fr.jcgay'
version = '0.3-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'fr.jcgay.send-notification', name: 'send-notification', version: '0.8'

    testCompile group: 'org.assertj', name: 'assertj-core', version: '1.7.1'
    testCompile group: 'org.spockframework', name:'spock-core', version: '1.0-groovy-2.3'
    testCompile group: 'com.netflix.nebula', name: 'nebula-test', version: '2.2.2'
    testCompile group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.12.1'
    testRuntime group: 'cglib', name: 'cglib-nodep', version: '3.1'
}

test {
    jvmArgs "-XX:MaxPermSize=256m"
}

if (project.hasProperty('nexusUsername')) {
    modifyPom {
        project {
            name 'gradle-notifier'
            packaging 'jar'
            description 'A plugin to have desktop notification when a build ends'
            url 'https://github.com/jcgay/gradle-notifier'

            scm {
                connection 'scm:git:git://github.com/jcgay/gradle-notifier.git'
                developerConnection 'scm:git:git@github.com:jcgay/gradle-notifier.git'
                url 'https://github.com/jcgay/gradle-notifier.git'
            }

            licenses {
                license {
                    name 'The MIT License'
                    url 'http://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id 'jcgay'
                    name 'Jean-Christophe Gay'
                    email 'contact@jeanchristophegay.com'
                }
            }
        }
    }

    task release(dependsOn: ['build', 'uploadArchives']) << {
        assert !(version as String).contains('SNAPSHOT') : "Project is a SNAPSHOT ($version). Cannot release"

        def grgit = org.ajoberstar.grgit.Grgit.open(project.file('.'))
        grgit.tag.add {
            name = "v$version"
            message = "Release $name"
        }
    }
}

pluginBundle {
    website = 'https://github.com/jcgay/gradle-notifier'
    vcsUrl = 'https://github.com/jcgay/gradle-notifier'
    description = 'A plugin to have desktop notification when a build ends'
    tags = ['notification', 'desktop', 'growl', 'snarl', 'notification center', 'anybar', 'notify-send', 'pushbullet', 'toaster']

    plugins {
        gradleNotifierPlugin {
            id = 'fr.jcgay.gradle-notifier'
            displayName = 'Gradle Notifier'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
